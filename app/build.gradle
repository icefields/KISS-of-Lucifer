buildscript {
    repositories {
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
    id 'net.ltgt.errorprone' version '4.3.0'
    id 'org.jetbrains.kotlin.android'
}

android {
    buildFeatures {
        buildConfig = true
    }
    defaultConfig {
        applicationId 'fr.neamar.kiss'
        minSdkVersion 21
        compileSdkVersion 36
        targetSdkVersion 36
        versionCode 217
        versionName "3.24.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        // isCoreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    lint {
        checkAllWarnings = true
        lintConfig = file('lint.xml')
    }

    namespace = 'fr.neamar.kiss'
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'

    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.21'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.1'
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:6.0.1')
    testImplementation 'org.hamcrest:hamcrest-library:3.0'

    errorprone('com.google.errorprone:error_prone_core:2.45.0')
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableWarningsInGeneratedCode = true
        disable('MissingOverride')
        disable('MissingSummary')
        disable('EmptyBlockTag')
        disable('EmptyCatch')
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
